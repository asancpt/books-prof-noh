Model 
COMM
  NPAR 6
  NSEC 4
  PNAM  'Ka', 'CLss', 'TLAG', 'CLpre', 'RKd', 'V'
  SNAM 'MRT1', 'MRT2', 'MRTenz', 'T12enz'
END
TEMP
END
FUNC1
I=0
J=1
L=CON(1)
REMA  CON(1) = NUMBER OF ADMINISTERED DOSES
GREEN:
REMARK GREEN LOOP CALCULATES HOW MANY DOSES TAKEN UP TO TIME X
I=I+1
J=J+2
IF X <= CON(J) THEN GOTO RED
ELSE IF I < L THEN GOTO GREEN
ENDIF
ENDIF
I=I+1
RED:
L=I-1
SUM=0
I=0
J=1
BLUE:
REMARK  BLUE LOOP USES SUPERPOSITION TO DETERMINE PREDICTED CONC.
I=I+1
J=J+2
T=X - CON(J) - TLAG
D=CON(J-1)
Dkor= 379.694
TBP=216.
TBP2=516.
        RKpre = CLpre/V
        RKss  = CLss/V
  IF X LT TBP THEN
     K10=RKpre
  ENDIF
  IF X GE TBP AND X LT TBP2 THEN
     K10=RKss - (RKss - RKpre)*DEXP(-RKd*(X - TBP))
  ENDIF
  IF X GE TBP2 THEN
     ass=RKss - (RKss - RKpre)*DEXP(-RKd*(TBP2 - TBP))
     K10=RKpre - (RKpre - ass)*DEXP(-RKd*(X - TBP2))
  ENDIF
COEF=D*Dkor*Ka/((Ka-K10)*V)
AMT=MAX(0,COEF*(DEXP(-K10*T)-DEXP(-Ka*T)))
SUM=SUM + AMT
IF I < L THEN GOTO BLUE
ENDIF
F=SUM
END
SECO
MRT1=1/Ka + 1/rkpre - tlag
MRT2=1/Ka + 1/rkss  - tlag
MRTENZ=1/rkd
T12enz=0.693/rkd
END
EOM
